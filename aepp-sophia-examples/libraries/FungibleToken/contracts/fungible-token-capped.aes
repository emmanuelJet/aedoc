contract FungibleCapped =
  record state = {
    owner            : address,
    cap              : int,
    total_supply      : int,
    map_balances         : map(address, int),
    map_allowed          : map((address,address), int)}

  public stateful function init(cap : int) : state = 
    require(cap > 0, "Cap can't be less than a zero")
    { owner       = Call.caller,
      cap         = cap,
      total_supply = 0,
      map_balances    = {},
      map_allowed     = {}}

  public function total_supply() : int =
    state.total_supply

  public function cap() : int =
    state.cap

  public function balance_of(who: address) : int = 
    Map.lookup_default(who, state.map_balances, 0)

  public function allowance(owner: address, spender: address) : int =
    Map.lookup_default((owner, spender), state.map_allowed, 0)

  public stateful function transfer(to: address, value: int) =
    transfer'(Call.caller, to, value)

  public stateful function approve(spender: address, value: int) = 
    require(value > 0, "Value is sub zero")
    require(spender != #0, "Invalid spender address")

    put(state{map_allowed[(Call.caller,spender)] = value})

  private stateful function transfer'(from: address, to: address, value: int) =
    require(value =< state.cap, "Value is bigger than the cap.")
    require(value > 0, "Value is sub zero")
    require(value =< balance_of(from), "Not enough balance")
    require(to != #0, "Invalid address")

    put(state{
      map_balances[from] = balance_of(from) - value,
      map_balances[to] = balance_of(to) + value})

  public stateful function transfer_from(from: address, to: address, value: int) =
    require(state.map_allowed[(from, Call.caller)] >= value, "Value is bigger than allowed")
      
    put(state{map_allowed[(from,Call.caller)] = state.map_allowed[(from,Call.caller)] - value})
    transfer'(from, to, value)

  public stateful function increase_allowance(spender: address, added_value: int) =
    require(spender != #0, "Invalid address")
    put(state{map_allowed[(Call.caller, spender)] = state.map_allowed[(Call.caller,spender)] + added_value})

  public stateful function decrease_allowance(spender: address, subtracted_value: int) =
    require(spender != #0, "Invalid address")
    put(state{map_allowed[(Call.caller,spender)] = state.map_allowed[(Call.caller,spender)] - subtracted_value})

  public stateful function mint(account: address, value: int) =
    only_owner()
    require(state.total_supply + value =< state.cap, "Total supply exceeds cap")
    require(account != #0, "Invalid address")

    put(state{total_supply = state.total_supply + value,
          map_balances[account] = balance_of(Call.caller) + value})

  public stateful function burn(value: int) =
    require(state.map_balances[Call.caller] >= value, "Burned amount is less than account balance")

    put(state{total_supply = state.total_supply - value,
          map_balances[Call.caller] = balance_of(Call.caller) - value})

  private function require(expression : bool, error : string) =
    if(!expression) 
      abort(error)

  private function only_owner() =
      require(Call.caller == state.owner, "Only owner can mint!")
