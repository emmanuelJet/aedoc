namespace Triple =

  function fst(t : ('a * 'b * 'c)) : 'a = switch(t)
    (x, _, _) => x

  function snd(t : ('a * 'b * 'c)) : 'b = switch(t)
    (_, y, _) => y

  function thd(t : ('a * 'b * 'c)) : 'c = switch(t)
    (_, _, z) => z


  function map1(f : 'a => 'm, t : ('a * 'b * 'c)) : ('m * 'b * 'c) = switch(t)
    (x, y, z) => (f(x), y, z)

  function map2(f : 'b => 'm, t : ('a * 'b * 'c)) : ('a * 'm * 'c) = switch(t)
    (x, y, z) => (x, f(y), z)

  function map3(f : 'c => 'm, t : ('a * 'b * 'c)) : ('a * 'b * 'm) = switch(t)
    (x, y, z) => (x, y, f(z))

  function trimap( f : 'a => 'x
                 , g : 'b => 'y
                 , h : 'c => 'z
                 , t : ('a * 'b * 'c)
                 ) : ('x * 'y * 'z) = switch(t)
    (x, y, z) => (f(x), g(y), h(z))

  function swap(t : ('a * 'b * 'c)) : ('c * 'b * 'a) = switch(t)
    (x, y, z) => (z, y, x)

  function rotr(t : ('a * 'b * 'c)) : ('c * 'a * 'b) = switch(t)
    (x, y, z) => (z, x, y)

  function rotl(t : ('a * 'b * 'c)) : ('b * 'c * 'a) = switch(t)
    (x, y, z) => (y, z, x)

